apply plugin: 'maven'
apply plugin: 'signing'

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}
task sourceJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: 'https://oss.sonatype.org/service/local/staging/deploy/maven2/') {
                authentication(userName: osshrUsername, password: osshrPassword)
            }

            pom.project {
                name "autoconfig-${project.name}"
                groupId = 'com.raniejaderamiso'
                url 'https://github.com/raniejade/autoconfig'

                scm {
                    connection 'scm:git:git@github.com:raniejade/autoconfig.git'
                }

                licenses {
                    license {
                        name 'MIT License'
                        url 'http://www.opensource.org/licenses/mit-license.php'
                    }
                }

                developers {
                    developer {
                        name 'Ranie Jade Ramiso'
                        email 'raniejaderamiso@gmail.com'
                    }
                }
            }
        }
    }
}

artifacts {
    archives javadocJar, sourceJar
}

gradle.taskGraph.whenReady { taskGraph ->
    if (taskGraph.allTasks.any { it instanceof Sign }) {
        allprojects { ext."signing.keyId" = signingKeyId }
        allprojects { ext."signing.secretKeyRingFile" = signingKeyFile }
        allprojects { ext."signing.password" = signingPassword }
    }
}


signing {
    sign configurations.archives
}
